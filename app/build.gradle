plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

android {
    def secretsFile = file("../secret.properties")
    Properties secrets = new Properties()

    if (secretsFile.exists()) {
        secrets.load(secretsFile.newDataInputStream())
    }

    compileSdk 32

    defaultConfig {
        applicationId "net.techbrewery.tvphotoframe"
        minSdk 29
        targetSdk 32
        versionCode 1
        versionName "1.0"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField "String", "REDIRECT_URI", "\"${secrets.getProperty("tvphotoframe.oauth.redirect.uri", System.getenv("TBTVPHOTOFRAME_OAUTH_REDIRECT_URI"))}\""
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        useIR = true
    }

    buildTypes {
//        release {
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.debug
//        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"958960862779-ip0na3ls312h6u32e9jejfam46a3jfsd.apps.googleusercontent.com\""
            buildConfigField "String", "OAUTH_CLIENT_SECRET", "\"${secrets.getProperty("tvphotoframe.oauth.client.secret", System.getenv("TBTVPHOTOFRAME_OAUTH_CLIENT_SECRET"))}\""
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += '/META-INF/DEPENDENCIES'
        }
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword secrets.getProperty("tvphotoframe.keystore.release.password", System.getenv("TBTVPHOTOFRAME_KEYSTORE_RELEASE_PWD"))
            keyAlias "tvphotoframe-release"
            keyPassword secrets.getProperty("tvphotoframe.keystore.release.password", System.getenv("TBTVPHOTOFRAME_KEYSTORE_RELEASE_PWD"))
        }
        debug {
            storeFile file("../debug.keystore")
            storePassword secrets.getProperty("tvphotoframe.keystore.debug.password", System.getenv("TBTVPHOTOFRAME_KEYSTORE_DEBUG_PWD"))
            keyAlias "tvphotoframe-debug"
            keyPassword secrets.getProperty("tvphotoframe.keystore.debug.password", System.getenv("TBTVPHOTOFRAME_KEYSTORE_DEBUG_PWD"))
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.leanback:leanback:1.0.0'

    implementation 'androidx.compose.ui:ui:1.0.5'
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.0.5'
    // Material Design
    implementation 'androidx.compose.material:material:1.0.5'
    implementation 'androidx.compose.material3:material3:1.0.0-alpha09'
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.0.5'
    implementation 'androidx.compose.material:material-icons-extended:1.0.5'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation 'com.google.api-client:google-api-client:1.31.5'
    implementation 'com.google.photos.library:google-photos-library-client:1.7.1'
    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-analytics'
    // Koin Core features
    implementation "io.insert-koin:koin-core:$koin_version"
    // Koin main features for Android
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.android.gms:play-services-auth:20.1.0'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.0.5'
    androidTestImplementation "androidx.test:core:1.4.0"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    // Koin Test features
    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
}